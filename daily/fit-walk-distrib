set terminal postscript
set xrange [0:24]
set yrange [0:0.1]

# cat ../../*/counts*csv | ./get-time-distrib2 -p | sort -n > distrib

print "gaussian_pdf(x, u, o) = exp(- (x - u) * (x - u) / (2 * o * o))/(o * sqrt(2 * pi))";
print "gaussian_cdf(x, u, o) = .5 * (.5 + erf((x - u) / (o * sqrt(2))))";

gaussian_pdf(x, u, o) = exp(- (x - u) * (x - u) / (2 * o * o))/(o * sqrt(2 * pi))
gaussian_cdf(x, u, o) = .5 * (.5 + erf((x - u) / (o * sqrt(2))))

f(x) = a * gaussian_pdf(x, u, o) + a1 * gaussian_pdf(x, u1, o1) + a2 * gaussian_pdf(x, u2, o2) + a3 * gaussian_pdf(x, u3, o3)
g(x) = a * gaussian_cdf(x, u, o) + a1 * gaussian_cdf(x, u1, o1) + a2 * gaussian_cdf(x, u2, o2) + a3 * gaussian_cdf(x, u3, o3)

a               = 0.719822
u               = 15.7166
o               = 4.31969
a1              = 0.168743
u1              = 7.75122
o1              = 1.16722
a2              = 0.0528317
u2              = 11.1177
o2              = 1.31161
a3              = 0.062617
u3              = 17.1517
o3              = 0.975906

fit f(x) + f(x + 24) + f(x - 24) "distrib" using 1:3 via a, u, o, a1, u1, o1, a2, u2, o2, a3, u3, o3
plot "distrib" using ($1 - 0.125):3 with steps title "Drive", f(x) + f(x + 24) + f(x - 24) lt 6
#plot g(x) + g(x + 24) + g(x - 24) - (g(0) + g(24) + g(-24))

print "drive(x) =   ",  a, " * gaussian_pdf(x, ",  u, ", ",  o, ") +"
print "             ", a1, " * gaussian_pdf(x, ", u1, ", ", o1, ") +"
print "             ", a2, " * gaussian_pdf(x, ", u2, ", ", o2, ") +"
print "             ", a3, " * gaussian_pdf(x, ", u3, ", ", o3, ");"




a               = 0.585846
u               = 16.2414
o               = 4.89947
a1              = 0.161598
u1              = 8.29638
o1              = 1.36408
a2              = 0.0912046
u2              = 12.2003
o2              = 1.21979
a3              = 0.162471
u3              = 16.6348
o3              = 1.99666



fit f(x) + f(x + 24) + f(x - 24) "distrib" using 1:2 via a, u, o, a1, u1, o1, a2, u2, o2, a3, u3, o3
plot "distrib" using ($1 - 0.125):2 with steps title "Walk", f(x) + f(x + 24) + f(x - 24) lt 6
#plot g(x) + g(x + 24) + g(x - 24) - (g(0) + g(24) + g(-24))

print "walk(x) =    ",  a, " * gaussian_pdf(x, ",  u, ", ",  o, ") +"
print "             ", a1, " * gaussian_pdf(x, ", u1, ", ", o1, ") +"
print "             ", a2, " * gaussian_pdf(x, ", u2, ", ", o2, ") +"
print "             ", a3, " * gaussian_pdf(x, ", u3, ", ", o3, ");"

# # biking, making no assumption about late night
# # but four peaks
#
# a               = 0.603252
# u               = 17.2
# o               = 3.07979
# a1              = 0.0765387
# u1              = 12.6958
# o1              = 1.21884
# a2              = 0.303162
# u2              = 8.63978
# o2              = 1.80507
# a3              = 0.0150229
# u3              = 17.3204
# o3              = 0.411049

fit f(x) + f(x + 24) + f(x - 24) "distrib" using 1:4 via a, u, o, a1, u1, o1, a2, u2, o2, a3, u3, o3

scale = 1 / (g(24) + g(24 + 24) + g(24 - 24) - (g(0) + g(24) + g(-24)))
a = a * scale
a1 = a1 * scale
a2 = a2 * scale
a3 = a3 * scale

plot "distrib" using ($1 - 0.125):($4 * scale) with steps title "Bike", f(x) + f(x + 24) + f(x - 24) lt 6
#plot g(x) + g(x + 24) + g(x - 24) - (g(0) + g(24) + g(-24))

print "bike(x) =    ",  a, " * gaussian_pdf(x, ",  u, ", ",  o, ") +"
print "             ", a1, " * gaussian_pdf(x, ", u1, ", ", o1, ") +"
print "             ", a2, " * gaussian_pdf(x, ", u2, ", ", o2, ") +"
print "             ", a3, " * gaussian_pdf(x, ", u3, ", ", o3, ");"

